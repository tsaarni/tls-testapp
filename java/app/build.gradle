plugins {
    id 'application'
    id "de.undercouch.download" version "5.5.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.24.3'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        //languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'tls.Tls'
}

// Uses https://github.com/michel-kraemer/gradle-download-task
task downloadAgent(type: Download) {
    src 'https://repo1.maven.org/maven2/name/neykov/extract-tls-secrets/4.0.0/extract-tls-secrets-4.0.0.jar'
    dest layout.buildDirectory
    overwrite false
}

tasks.withType(JavaExec) {
    // Enable TLS secrets extraction.
    if (project.hasProperty('useAgent')) {
        dependsOn downloadAgent
        jvmArgs "-javaagent:build/extract-tls-secrets-4.0.0.jar=${rootDir}/wireshark-keys.log"
    }

    // Enable TLS debug logging.
    if (project.hasProperty('tlsDebug')) {
        jvmArgs '-Djavax.net.debug=all'
    }

    // Use custom java.security properties file.
    //jvmArgs "-Djava.security.properties=${rootDir}/java.security"

    // Enable automatic CRL downloading and checking.
    // Run web server for CRLDP with
    //    python3 -m http.server 8080 --directory certs
    // and connect to server with revoked certificate:
    //    openssl s_client -connect localhost:14443 -cert certs/revoked-client.pem -key certs/revoked-client-key.pem -CAfile certs/server-ca.pem
    //jvmArgs '-Dcom.sun.net.ssl.checkRevocation=true'
    //jvmArgs '-Dcom.sun.security.enableCRLDP=true'
}

task server(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.tsaarni.tlstestapp.tls.Tls'
    args 'server'
}

task client(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.tsaarni.tlstestapp.tls.Tls'
    args 'client'
}

task ciphers(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.tsaarni.tlstestapp.ciphers.Ciphers'
}
