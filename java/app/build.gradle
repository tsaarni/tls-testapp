plugins {
    id 'application'
    id "de.undercouch.download" version "5.5.0"
}

repositories {
    mavenCentral()
}

dependencies {
    runtimeOnly 'org.logevents:logevents:0.5.2'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.20.0'
    implementation 'fi.protonode:certy:0.3.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        //languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'tls.Tls'
}

task generateCerts(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'testapp.GenerateCerts'
    args 'testapp'
}

// Uses https://github.com/michel-kraemer/gradle-download-task
task downloadAgent(type: Download) {
    src 'https://repo1.maven.org/maven2/name/neykov/extract-tls-secrets/4.0.0/extract-tls-secrets-4.0.0.jar'
    dest layout.buildDirectory
    overwrite false
}

task server(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'tls.Tls'
    args 'server'
    // enable TLS debug logging
    //jvmArgs '-Djavax.net.debug=all'
}

task serverWithAgent(type: JavaExec) {
    dependsOn downloadAgent
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'tls.Tls'
    args 'server'
    jvmArgs "-javaagent:build/extract-tls-secrets-4.0.0.jar=/tmp/wireshark-keys.log"
}

task client(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'tls.Tls'
    args 'client'
}

task ciphers(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'ciphers.Ciphers'
}
